#define MAX_NELS_PER_CELL 62 //Should be a power of two minus two for better alignment

typedef struct{
	float4 v0;
	float4 v1;
	float4 v2;
} Triangle;

//A light path defined by 5 vertices
//The starting point is the light source position, but we do not need to memorize it
//The path length is defined as the number of edges
typedef struct{
	float4 v[4];
	uchar length;
} Path;

typedef struct{
	float4 vmin;
	float4 vmax;
} Box;

typedef struct{
	uint nels;
	ushort elem_index[MAX_NELS_PER_CELL];
} Cell;

//MWC64x, an RNG made by David B. Tomas, with custom seeding
//Source: http://cas.ee.ic.ac.uk/people/dt10/research/rngs-gpu-mwc64x.html

typedef struct{ uint2 x; uint2 c; } mwc64xvec2_state_t;

inline float2 MWC64XVEC2(mwc64xvec2_state_t *s, float leftLimit, float rightLimit)
{
    enum{ MWC64XVEC2_A = 4294883355U };
    uint2 x=s->x, c=s->c;
    uint2 res=x^c;                     // Calculate the result
    uint2 hi=mul_hi(x,MWC64XVEC2_A);              // Step the RNG
    x=x*MWC64XVEC2_A+c;
    c=hi+convert_uint2(x<c);
    s->x=x;
    s->c=c;             // Pack the state back up
    return leftLimit + convert_float2(res)*((rightLimit - leftLimit)/4294967295);
}

//Another simple RNG (often used in hashing) to randomize local id or global id
inline uint randomizeId(uint id)
{
        id = (id ^ 61) ^ (id >> 16);
        id *= 9;
        id = id ^ (id >> 4);
        id *= 0x27d4eb2d;
        id = id ^ (id >> 15);
        return id;
 }

//Mix seeds with randomized id
inline void MWC64XVEC2_Seeding(mwc64xvec2_state_t *s, uint4 seeds){
	const int i = get_global_id(1) * get_global_size(0) + get_global_id(0);
	s->x = (uint2)((seeds.x) ^ randomizeId(i), (seeds.y) ^ randomizeId(i));
	s->c = (uint2)((seeds.z) ^ randomizeId(i), (seeds.w) ^ randomizeId(i));
}

//Defined as operator! in the simple CPU tracer
inline float4 Normalize(float4 x){
	return ((1/sqrt(dot(x, x))) * x);
}

inline int TraceRay(float4 origin, float4 direction, float * t, float4 * normal, 
	local int * restrict Spheres, local int * restrict Squares, 
	global const Triangle * restrict Triangles, int ntriangles){

	int m = 0;	//default material
	float rayDist;
	float4 intersection;

	//Triangle check vars
	Triangle curr_triangle;
	float4 edge0, edge2; 
	//Moller-Trumbore solution
	float4 pvec, qvec, tvec;
	float det, invDet, barycentric_u, barycentric_v;

	//Check for floor intersection
	rayDist = -origin.z / direction.z;
	if(.01f < rayDist && rayDist < *t){
		*t = rayDist;
		*normal = (float4)(0, 0, 1, 0);
		m = 1;
	}
	
	//Check for square intersection
	for(int k = 19; k--;){
		for(int j = 9; j--;){
			if(Squares[j] & 1 << k){
				rayDist = (4+j-origin.z)/direction.z;
				intersection = origin + direction * rayDist;
				if(rayDist < *t && (fabs(k-intersection.x)<1) && fabs(intersection.y)<1){
				//if(dist < *t && distance(intersection, (float4)(k, 0, j+4, 0))<2){	//Circle intersection with euclidean distance
					*t = rayDist;
					*normal = (float4)(0, 0, 1, 0);
					m = 3;
				}
			}
		}
	}
	//Check for sphere intersection
	for(int k = 19; k--;){
		for(int j = 9; j--;){
			if (Spheres[j] & 1 << k){
				float4 p = origin + (float4)(-k, 0, -j - 4, 0);
				float b = dot(p, direction);
				float c = dot(p, p) - 1;
				float q = b * b - c;

				//Does the ray hit the sphere?
				if(q > 0){
					rayDist = -b - sqrt(q);
					//It does, compute the distance camera-sphere
					if(rayDist < (*t) && rayDist > 0.01f){
						*t = rayDist;
						*normal = Normalize(p + direction * (*t));
						m = 3;
					}
				}
			}
		}
	}
	
	//Check for triangle intersection (Moller-Trumbore)
	for(int i=0; i<ntriangles; i++){
		curr_triangle = Triangles[i];
		edge0 = curr_triangle.v1 - curr_triangle.v0;
		edge2 = curr_triangle.v2 - curr_triangle.v0;

		pvec = cross(direction, edge2);
		det = dot(edge0, pvec);
		if(fabs(det) < 0.01f)	continue;
		invDet = 1/det;
		tvec = origin - curr_triangle.v0;
		barycentric_u = dot(tvec, pvec) * invDet;
		if(barycentric_u < 0 || barycentric_u > 1) continue;
		qvec = cross(tvec, edge0);
		barycentric_v = dot(direction, qvec) * invDet;
		if (barycentric_v < 0 || barycentric_u+barycentric_v > 1) continue;
		rayDist = dot(edge2, qvec) * invDet;

		//Ray hits the triangle
		if(rayDist < *t){
			*t = rayDist;
			*normal = Normalize(cross(edge0, edge2));
			m = 4;
		}
	}
	
	return m;
}

inline float4 GetRandomDirection(mwc64xvec2_state_t rng){
	//Random direction algorithm from https://math.stackexchange.com/questions/44689/how-to-find-a-random-axis-or-unit-vector-in-3d/182650#182650
	float randSum = 2;
	float2 randValues;
	while(randSum >= 1){
		randValues = MWC64XVEC2(&rng, -1.0f, 1.0f);
		randSum = randValues.x*randValues.x + randValues.y*randValues.y;
	}
	return (float4)(2*randValues.x*sqrt(1-randSum), 2*randValues.y*sqrt(1-randSum), 1-2*randSum, 0);
}

inline int AddRandomVertex(float4 origin, float4 * vertexToAdd, Path * path, 
	local int * restrict lSpheres, local int * restrict lSquares, 
	global const Triangle * restrict lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	const float4 direction = GetRandomDirection(rng);
	float4 normal;
	float t = 1e9;	//default distance
	if(TraceRay(origin, direction, &t, &normal, lSpheres, lSquares, lTriangles, ntriangles)){
		*vertexToAdd = origin + direction * t;
		path->length += 1;
		return 1;
	}
	return 0;
}

inline Path GetRandomPath(float4 origin, local int * restrict lSpheres, local int * restrict lSquares, 
	global const Triangle * restrict lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	Path curr_path;
	curr_path.length = 0;	//Default path length
	float4 curr_origin = origin;
	for(int i=0; i<4; ++i){
		if(!AddRandomVertex(curr_origin, &(curr_path.v[i]), &curr_path, lSpheres, lSquares, lTriangles, ntriangles, rng)) break;
		curr_origin = curr_path.v[i];
	}
	return curr_path;
}

//Commonly used perturbations from http://cg.iit.bme.hu/~szirmay/paper50_electronic.pdf
inline float4 Perturbation(float4 vertex, mwc64xvec2_state_t rng){
	float4 mutatedVertex;	
	const float4 randValues = (float4)(MWC64XVEC2(&rng, 0.0f, 1.0f),MWC64XVEC2(&rng, 0.0f, 1.0f).x, 0);
	const float4 s0 = (float4)(1.0f, 1.0f, 1.0f, 0);
	const float4 s1 = (float4)(1.0f/512.0f, 1.0f/512.0f, 1.0f/512.0f, 0);
	const float4 s2 = (float4)(1.0f/16.0f, 1.0f/16.0f, 1.0f/16.0f, 0);
	const float4 dx = s1 / (s1 / s2 + fabs(2.0f * randValues - s0)) - s1 / (s1 / s2 + s0);

	//Mutate vertex.x
	if (randValues.x < 0.5f){
		if(vertex.x < 1.0f) mutatedVertex.x = vertex.x + dx.x;
		else mutatedVertex.x = vertex.x + dx.x - 1.0f;
	}
	else{
		if(vertex.x < 0.0f) mutatedVertex.x = vertex.x - dx.x + 1.0f;
		else mutatedVertex.x = vertex.x - dx.x;
	}
	//Mutate vertex.y
	if (randValues.y < 0.5f){
		if(vertex.y < 1.0f) mutatedVertex.y = vertex.y + dx.y;
		else mutatedVertex.y = vertex.y + dx.y - 1.0f;
	}
	else{
		if(vertex.y < 0.0f) mutatedVertex.y = vertex.y - dx.y + 1.0f;
		else mutatedVertex.y = vertex.y - dx.y;
	}
	//Mutate vertex.z
	if (randValues.z < 0.5f){
		if(vertex.z < 1.0f) mutatedVertex.z = vertex.z + dx.z;
		else mutatedVertex.z = vertex.z + dx.z - 1.0f;
	}
	else{
		if(vertex.z < 0.0f) mutatedVertex.z = vertex.z - dx.z + 1.0f;
		else mutatedVertex.z = vertex.z - dx.z;
	}
	mutatedVertex.w = 0.0f;
	return mutatedVertex;
	
}

//Check if destination is the first intersection point of a ray that starts from origin in the direction Normalize(destination - origin)
inline int VerifyIntersection(float4 origin, float4 destination, local int * restrict lSpheres, 
	local int * restrict lSquares, global const Triangle * restrict lTriangles, int ntriangles){
	float t;
	float4 normal;
	const float4 direction = Normalize(destination - origin);
	const int m = TraceRay(origin, direction, &t, &normal, lSpheres, lSquares, lTriangles, ntriangles);
	if (!m) return false;
	else{
		const float4 intersection = origin + direction * t;
		return (destination.x == intersection.x && destination.y == intersection.y && destination.z == intersection.z);
	}
}

//Mutate by adding vertices and perturbation
inline void Mutate(Path * seedpath, float4 origin, 
	local int * restrict lSpheres, local int * restrict lSquares, 
	global const Triangle * restrict lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	if(seedpath->length == 0){	//Path is empty, try and make a new one
		*seedpath = GetRandomPath(origin, lSpheres, lSquares, lTriangles, ntriangles, rng);
		if(seedpath->length == 0) return;	//Still empty, try again next round
	}
	float2 randValues = MWC64XVEC2(&rng, 0.0f, 1.0f);
	const float mutation_probability = 1.0f/(seedpath->length+0.2f);
	if(mutation_probability < randValues.x) return;
	Path temp_path;
	temp_path.length = 0;
	float4 curr_origin = origin;
	for(int i=0; i<seedpath->length; ++i){
		temp_path.v[i] = Perturbation(seedpath->v[i], rng);
		if (VerifyIntersection(curr_origin, temp_path.v[i], lSpheres, lSquares, lTriangles, ntriangles)){
			temp_path.length++;
			curr_origin = temp_path.v[i];
		} else break;
	}
	if(temp_path.length == seedpath->length){
		*seedpath = temp_path;
	}
	if (seedpath->length == 1){
		//Try adding 1 (40%), 2 (20%) or 3 (10%) vertices
		if (randValues.y > 0.3f){
			if(!AddRandomVertex(seedpath->v[0], &(seedpath->v[1]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng)) return;
		}
		if (randValues.y > 0.7f){
			if(!AddRandomVertex(seedpath->v[1], &(seedpath->v[2]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng)) return;
		}
		if (randValues.y > 0.9f) AddRandomVertex(seedpath->v[2], &(seedpath->v[3]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng);
	}
	else if (seedpath->length == 2){
		//Try adding 1 (30%) or 2 (20%) vertices
		if (randValues.y < 0.3f){
			if(!AddRandomVertex(seedpath->v[1], &(seedpath->v[2]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng)) return;
		}
		if (randValues.y < 0.2f) AddRandomVertex(seedpath->v[2], &(seedpath->v[3]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng);
	}
	else if (seedpath->length == 3){
		//Try adding 1 vertex (20%)
		if (randValues.y < 0.2f) AddRandomVertex(seedpath->v[2], &(seedpath->v[3]), seedpath, lSpheres, lSquares, lTriangles, ntriangles, rng);
	}
}

inline float4 Sample(float4 * origin, float4 * direction, mwc64xvec2_state_t * rng, 
	local int * restrict Spheres, local int * restrict Squares, 
	global const Triangle * restrict Triangles, int ntriangles,
	global const float4 * restrict virtual_point_lights, int nvirtuallights, 
	global const Cell * VLPsGrid, float4 VLPsBoxMin, float4 cell_size, int4 grid_res, 
	local float4 * restrict scenelights, int nlights){
	//Recursion vars
	float4 colorFact = (float4)(0, 0, 0, 0);
	int divFact = 1;

	float2 randValues;
	float4 intersection, half_vec;
	float t;

	float4 normal, light_dir, light_pos;
	float distanceFromLight, light_intensity;
	float lamb_f, color, total_illumination = 0.0f;

	int material;
	for(int maxIter = 5; maxIter--;){
		t = 1e9;	//default distance
		material = TraceRay(*origin, *direction, &t, &normal, Spheres, Squares, Triangles, ntriangles);
		if (!material){
			//Nothing found and the ray goes upward: Generate a sky color
			return colorFact + (float4)(0.7f, 0.6f, 1.0f, 0) * pow(1 - (*direction).z, 4) / divFact;
		}

		//Something was hit
		intersection = (*origin) + (*direction) * t;
		//Compute potential cell index
		const int4 index3D = convert_int4((intersection-VLPsBoxMin)/cell_size);
		const int index = index3D.z*grid_res.x*grid_res.y + index3D.y*grid_res.x + index3D.x;
		if (index > 0 && index < grid_res.x * grid_res.y * grid_res.z){	//Intersection is inside the grid
			Cell c = VLPsGrid[index];
			//Compute total illumination factor by checking all virtual point lights in the cell
			for(uint i=0; i<c.nels; ++i){
				light_pos = virtual_point_lights[c.elem_index[i]];
				light_intensity = light_pos.w;
				if(light_intensity <= 0) continue;
				light_pos.w = 0;
				distanceFromLight = distance(light_pos, intersection);
				light_dir = (light_pos - intersection)/distanceFromLight;

				//Calculate the lambertian factor
				lamb_f = dot(light_dir, normal);
				t = distanceFromLight;
				//Calculate illumination factor (lambertian coefficient > 0 or in shadow)?
				//...Or just clamp it, shadows make it painfully slow because of the number of VLPs
				//if(lamb_f < 0 || TraceRay(intersection, light_dir, &t, &half_vec, Spheres, Squares, Triangles, ntriangles)){
				if(lamb_f < 0){
					lamb_f = 0;
				}
				else{
					//Objects away from the light should have less illumination (Inverse square law)
					total_illumination += lamb_f * min(light_intensity/(distanceFromLight*distanceFromLight), 1.0f);
				}
			}
		}

		if(total_illumination > 1.0f) total_illumination = 1.0f;
		
		//Compute soft shadows with real lights
		for(int i=0; i<nlights; ++i){
			light_pos = scenelights[i];
			randValues = MWC64XVEC2(rng, 0.0f, 1.0f);
			light_pos.w = 0;
			distanceFromLight = distance(light_pos, intersection);
			light_dir = Normalize(light_pos + (float4)(randValues,0,0) + intersection * (-1));
			t = distanceFromLight;
			if(TraceRay(intersection, light_dir, &t, &half_vec, Spheres, Squares, Triangles, ntriangles)){
				total_illumination -= 1.0f/nlights;
			}
		}
		total_illumination /= 4;

		if(material == 1){
			//Nothing was hit and the ray was going downward: Generate floor checkerboard texture
			intersection = intersection * 0.2f;
			return colorFact+((int)(ceil(intersection.x) + ceil(intersection.y)) & 1 ? (float4)(3, 1, 1, 0) : (float4)(3, 3, 3, 0)) * (total_illumination) / divFact;
		}
		if(material == 3){	//diffuse shader
			float4 diffuseColor = (float4)(2, 3, 2, 0);
			return colorFact + (diffuseColor * (total_illumination)) / divFact;
		}
		if(material == 4){	//facing ratio
			return colorFact + max(0.0f, dot(normal, -(*direction)))/ divFact;
		}
		//m == 2 A reflective surface was hit. Cast a ray bouncing from it.
		//Attenuate color by 50% since it is bouncing (* 0.5)
		//Unrolled recursion with a loop and by updating those factors
		else{
			half_vec = (*direction) + normal * (dot(normal, *direction) * (-2));
			color = pow(dot(light_dir, half_vec) * (total_illumination > 0), 99);
			colorFact += (float4)(color, color, color, 0) * divFact;
			*origin = intersection;
			*direction = half_vec;
			divFact *= 2;
		}
	}
}

inline float4 SampleFromLightSource(float4 origin, float4 direction, 
	local int * restrict Spheres, local int * restrict Squares, 
	global const Triangle * restrict Triangles, int ntriangles,
	float light_intensity, int total_paths){

	float t;

	float4 normal, intersection;
	float distanceFromLight, lamb_f;

	t = 1e9;	//default distance
	int material = TraceRay(origin, direction, &t, &normal, Spheres, Squares, Triangles, ntriangles);
	intersection = origin + direction * t;
	if (!material){
		//No surface or floor found: return dummy light source
		return intersection;
	}

	//Something was hit
	//Calculate the lambertian factor
	lamb_f = dot(direction, normal);

	//Clamp to 0
	if(lamb_f < 0){
		lamb_f = 0;
	}
	else{
		//Objects away from the light should have less illumination (Inverse square law)
		distanceFromLight = distance(origin, intersection);
		lamb_f = lamb_f * min(light_intensity/(distanceFromLight*distanceFromLight), 1.0f);
	}
	//Clamp to 1
	if(lamb_f > 1.0f) lamb_f = 1.0f;

	//return a light at position intersection with an intensity inversely proportional to the number of VLPs
	if(material == 1){
		//The ray was going downward: return floor light
		return (float4)(intersection.s012, 100*lamb_f/(total_paths/256));
	}
	if(material == 2){
		//The ray hits a specular surface, which should reflect most light: return a dim light
		return (float4)(intersection.s012, 10*lamb_f/(total_paths/256));
	}
	if(material == 3){	//diffuse shader
		return (float4)(intersection.s012, 40*lamb_f/(total_paths/256));
	}
	return (float4)(0, 0, 0, 0);
}

//Compute random paths through the scene as seeds for the second phase
kernel void lightTracer(global const int * restrict Spheres, global const int * restrict Squares, 
	global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict scenelights, int nlights,
	global Path * restrict seedpaths, uint4 seeds,
	local int * restrict lSpheres, local int * restrict lSquares, local float4 * restrict lScenelights){
	int gi = get_global_id(0);
	int gws = get_global_size(0);
	int li = get_local_id(0);

	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);

	float4 current_light;
	float4 origin;

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lSquares[li]=Squares[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}

	barrier(CLK_LOCAL_MEM_FENCE);
	//for each light, create a path launching rays in random directions
	for(int l=0; l<nlights; ++l){
		current_light = lScenelights[l];
		origin = (float4)(current_light.s012, 0);	//Get position in 3D space of current light
		seedpaths[gi+l*gws] = GetRandomPath(origin, lSpheres, lSquares, Triangles, ntriangles, rng);
	}
}

//Uses seed paths from lightTracer to find more light paths
kernel void MetropolisLightTracer(global const int * restrict Spheres, 
	global const int * restrict Squares, 
	global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict scenelights, int nlights,
	global Path * restrict seedpaths, global float16 * restrict virtual_point_lights, 
	uint4 seeds, int mutation_rounds,
	local int * restrict lSpheres, local int * restrict lSquares, local float4 * restrict lScenelights){
	int gi = get_global_id(0);
	int gws = get_global_size(0);
	int li = get_local_id(0);
	int total_paths = gws * nlights;	//Total number of paths

	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);

	float4 current_light;
	float light_intensity;
	float4 origin, direction;

	Path seedpath;
	float4 curr_vlp[4];

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lSquares[li]=Squares[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}
	barrier(CLK_LOCAL_MEM_FENCE);
	//for each light, create the sample VLPs from the mutated seed path
	for(int l=0; l<nlights; ++l){
		current_light = lScenelights[l];
		origin = (float4)(current_light.s012, 0);	//Get position in 3D space of current light
		light_intensity = current_light.w;
		seedpath = seedpaths[gi+l*gws];

		//Initialize lights in case the path gets cut off
		curr_vlp[0] = (float4)(0);
		curr_vlp[1] = (float4)(0);
		curr_vlp[2] = (float4)(0);
		curr_vlp[3] = (float4)(0);

		for(int m = 0; m < mutation_rounds; ++m){
			Mutate(&seedpath, origin, lSpheres, lSquares, Triangles, ntriangles, rng);
		}
		//Get a light sample from each vertex
		for(int i=0; i<seedpath.length; ++i){
			direction = Normalize(seedpath.v[i] - origin);
			curr_vlp[i] = SampleFromLightSource(origin, direction, lSpheres, lSquares, Triangles, ntriangles, light_intensity/(1 << i), total_paths);
			if (curr_vlp[i].w == 0) break;
			origin = seedpath.v[i];
		}
		virtual_point_lights[gi+l*gws] = *((float16*)curr_vlp);
	}
}

//Reduction to compute VLP bounding box
kernel void reduceMinAndMax_lmem(global float4 * v1, global float8 * v2,
	local float8 * lmem, int N_VLP){

	const int gi = get_global_id(0);
	if (gi >= N_VLP) return;

	const float4 vlp = v1[gi];
	const float4 vlp_pos = (float4)(vlp.s012, 0);
	const float vlp_intensity = vlp.w;
	const float radius = 16*sqrt(vlp_intensity);
	const float4 radius_vec = (float4)(radius, radius, radius, 0);
	float4 lmin = vlp_pos - radius_vec;
	float4 lmax = vlp_pos + radius_vec;
	
	float8 lmemValue = (float8)(lmin, lmax);

	const int i = get_local_id(0);
	lmem[i] = lmemValue;
	int working = get_local_size(0) >> 1;
	while(working > 0){
		barrier(CLK_LOCAL_MEM_FENCE);
		if (i < working) {
			lmemValue = lmem[i+working];
			const int4 lessComparison = (int4)(isless(lmemValue.lo, lmin) << 31);
			const int4 greaterComparison = (int4)(isgreater(lmemValue.hi, lmax) << 31);
			lmin = select(lmin, lmemValue.lo, lessComparison);
			lmax = select(lmax, lmemValue.hi, greaterComparison);
			lmem[i]=(float8)(lmax, lmin);
		}
		working >>= 1;
	}
	if (i == 0){
		v2[get_group_id(0)] = (float8)(lmin, lmax);
	}

}

//Reduce nwg couples (lmin, lmax) to the actual bounding box values (vmin, vmax)
//After this, v2 will have at the beginning (vmin, vmax), we can just treat it as a Box pointer
kernel void reduceMinAndMax_lmem_nwg(global float8 * v1, global float8 * v2,
	local float8 * lmem, int N_VLP){

	const int gi = get_global_id(0);
	if (gi >= N_VLP) return;
	float8 lmemValue = v1[gi];
	float4 lmin = lmemValue.lo;
	float4 lmax = lmemValue.hi;

	const int i = get_local_id(0);
	lmem[i] = lmemValue;
	int working = get_local_size(0) >> 1;
	while(working > 0){
		barrier(CLK_LOCAL_MEM_FENCE);
		if (i < working) {
			lmemValue = lmem[i+working];
			const int4 lessComparison = (int4)(isless(lmemValue.lo, lmin) << 31);
			const int4 greaterComparison = (int4)(isgreater(lmemValue.hi, lmax) << 31);
			lmin = select(lmin, lmemValue.lo, lessComparison);
			lmax = select(lmax, lmemValue.hi, greaterComparison);
			lmem[i]=(float8)(lmax, lmin);
		}
		working >>= 1;
	}
	if (i == 0){
		v2[get_group_id(0)] = (float8)(lmin, lmax);
	}

}

inline void atomic_addVLP(volatile global Cell* c, const int VLP_ID){
	int old = atomic_inc(&(c->nels));
	if (old >= MAX_NELS_PER_CELL) return;
	c->elem_index[old] = VLP_ID;
}

kernel void initVLPsGrid(global Cell * restrict VLPsGrid, global const float4 * restrict virtual_point_lights, const float4 VLPsBoxMin, const int4 grid_res, const float4 cell_size){
	const int gi = get_global_id(0);
	barrier(CLK_GLOBAL_MEM_FENCE);
	const float4 vlp = virtual_point_lights[gi];
	const float4 vlp_pos = (float4)(vlp.s012, 0);
	const float vlp_intensity = vlp.w;
	//Light bounding box is trivial: estimate light radius r to be 16*sqrt(light_intensity), vmin is vlp_pos-r and vmax is vlp_pos+r
	const float radius = 16*sqrt(vlp_intensity);
	const float4 radiusVec = (float4)(radius, radius, radius, 0);
	//Convert to cell coordinates
	const int4 min = clamp(convert_int4(((vlp_pos-radiusVec)-VLPsBoxMin)/cell_size), (int4)(0), grid_res-(int4)(1, 1, 1, 0));
	const int4 max = clamp(convert_int4(((vlp_pos+radiusVec)-VLPsBoxMin)/cell_size), (int4)(0), grid_res-(int4)(1, 1, 1, 0));
	for(int z = min.z; z <= max.z; ++z){
		for(int y = min.y; y <= max.y; ++y){
			for(int x = min.x; x <= max.x; ++x){
				const int index = z*grid_res.x*grid_res.y + y*grid_res.x + x;
				atomic_addVLP((volatile global Cell*)(VLPsGrid+index), gi);
			}
		}
	}
}

kernel void pathTracer(global uchar4 * restrict img, global const int * restrict Spheres, 
	global const int * restrict Squares, global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict virtual_point_lights, int nvlp, 
	global const Cell * VLPsGrid, float4 VLPsBoxMin, float4 cell_size, int4 grid_res,
	global const float4 * restrict scenelights, int nlights,
	float4 cam_forward, float4 cam_up, float4 cam_right, float4 eye_offset, uint4 seeds,
	local int * restrict lSpheres, local int * restrict lSquares, local float4 * restrict lScenelights){
	float4 color = (float4)(13, 13, 13, 0);
	int i = get_global_id(0);
	int j = get_global_id(1);
	int li = get_local_id(0) + get_local_id(1) * get_local_size(0);
	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);
	float4 randValues;
	float4 origin, direction, delta;

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lSquares[li]=Squares[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}
	barrier(CLK_LOCAL_MEM_FENCE);
	for(int r = 64; r--;){
		randValues = (float4)(MWC64XVEC2(&rng, 0.0f, 1.0f), MWC64XVEC2(&rng, 0.0f, 1.0f));
		delta = cam_up * ((randValues.x - 0.5f) * 99) + cam_right * ((randValues.y - 0.5f) * 99);
		origin = (float4)(17, 16, 8, 0) + delta;
		direction = Normalize(delta * (-1) + (cam_up * (randValues.z + i) + cam_right * (j + randValues.w) + eye_offset) * 16);
		color = Sample(&origin, &direction, &rng, lSpheres, lSquares, Triangles, ntriangles, virtual_point_lights, nvlp, VLPsGrid, VLPsBoxMin, cell_size, grid_res, lScenelights, nlights) * 3.5f + color;
	}
	color.w = 255;
	img[j*get_global_size(0)+i]=convert_uchar4(color);
}

