//MWC64x, an RNG made by David B. Tomas, with custom seeding
//Source: http://cas.ee.ic.ac.uk/people/dt10/research/rngs-gpu-mwc64x.html

typedef struct{ uint2 x; uint2 c; } mwc64xvec2_state_t;

enum{ MWC64XVEC2_A = 4294883355U };
enum{ MWC64XVEC2_M = 18446383549859758079UL };

inline float2 MWC64XVEC2(mwc64xvec2_state_t *s, float leftLimit, float rightLimit)
{
    uint2 x=s->x, c=s->c;
    uint2 res=x^c;                     // Calculate the result
    uint2 hi=mul_hi(x,MWC64XVEC2_A);              // Step the RNG
    x=x*MWC64XVEC2_A+c;
    c=hi+convert_uint2(x<c);
    s->x=x;
    s->c=c;             // Pack the state back up
    return leftLimit + convert_float2(res)*((rightLimit - leftLimit)/4294967295);
}

//Another simple RNG (often used in hashing) to randomize local id or global id
inline uint randomizeId(uint id)
{
        id = (id ^ 61) ^ (id >> 16);
        id *= 9;
        id = id ^ (id >> 4);
        id *= 0x27d4eb2d;
        id = id ^ (id >> 15);
        return id;
 }

//Mix seeds with randomized global ids
inline void MWC64XVEC2_Seeding(mwc64xvec2_state_t *s, uint4 seeds){
	const int gi = get_global_id(0);
	//const int li = get_local_id(0) + 1;
	const int li = get_global_id(1);
	s->x = (uint2)((seeds.x) ^ randomizeId(gi&li), (seeds.y) ^ randomizeId(gi+li));
	s->c = (uint2)((seeds.w) ^ randomizeId(gi+li), (seeds.z) ^ randomizeId(gi&li));
}

kernel void imginit_buf(global uchar4 * img)
{
	const int j = get_global_id(1);
	const int i = get_global_id(0);
	const int size = get_global_size(0);

	img[j*size+i] = (uchar4)(255, 255, 255, 255);
}

//Defined as operator! in the simple CPU tracer
inline float4 NotOperator(float4 x){
	return ((1/sqrt(dot(x, x))) * x);
}

inline int TraceRay(float4 origin, float4 destination, float * t, float4 * normal, global int * G){

	*t = 1e9;
	int m = 0;
	float p1 = -origin.z / destination.z;
	if(.01f < p1){
		*t = p1;
		*normal = (float4)(0, 0, 1, 0);
		m = 1;
	}
	for(int k = 19; k--;){
		for(int j = 9; j--;){
			if (G[j] & 1 << k){
				float4 p = origin + (float4)(-k, 0, -j - 4, 0);
				float b = dot(p, destination);
				float c = dot(p, p) - 1;
				float q = b * b - c;

				//Does the ray hit the sphere?
				if(q > 0){
					float s = -b - sqrt(q);
					//It does, compute the distance camera-sphere
					if(s < (*t) && s > 0.01f){
						*t = s;
						*normal = NotOperator(p + destination * (*t));
						m = 2;
					}
				}
			}
		}
	}
	return m;
}

inline float4 Sample(float4 * origin, float4 * destination, mwc64xvec2_state_t * rng, global int * G){
	//Recursion vars
	float4 colorFact = (float4)(0, 0, 0, 0);
	int divFact = 1;

	float2 randValues;
	float4 intersection, half_vec;
	float t;
	float4 normal, light_dir;
	float lamb_f, color;
	int match;
	while(true){
		randValues = MWC64XVEC2(rng, 0.0f, 1.0f);
		match = TraceRay(*origin, *destination, &t, &normal, G);
		if (!match){
			//No sphere found and the ray goes upward: Generate a sky color
			return colorFact + (float4)(0.7f, 0.6f, 1.0f, 0) * pow(1 - (*destination).z, 4) / divFact;
		}

		//A sphere was maybe hit
		intersection = (*origin) + (*destination) * t;
		light_dir = NotOperator((float4)(9 + randValues.x, 9 + randValues.y, 16, 0) + intersection * (-1));
		half_vec = (*destination) + normal * (dot(normal, *destination) * (-2));

		//Calculate the lambertian factor
		lamb_f = dot(light_dir, normal);

		//Calculate illumination factor (lambertian coefficient > 0 or in shadow)?
		if(lamb_f < 0 || TraceRay(intersection, light_dir, &t, &normal, G)){
			lamb_f = 0;
		}

		color = pow(dot(light_dir, half_vec) * (lamb_f > 0), 99);

		if(match & 1){
			//No sphere was hit and the ray was going downward: Generate a floor color
			intersection = intersection * 0.2f;
			return colorFact+((int)(ceil(intersection.x) + ceil(intersection.y)) & 1 ? (float4)(3, 1, 1, 0) : (float4)(3, 3, 3, 0)) * (lamb_f * 0.2f + 0.1f) / divFact;
		}
		//m == 2 A sphere was hit. Cast a ray bouncing from the sphere surface.
		//Attenuate color by 50% since it is bouncing (* 0.5)
		//Unrolled recursion with an infinite loop and by updating those factors
		else{
			colorFact += (float4)(color, color, color, 0) * divFact;
			*origin = intersection;
			*destination = half_vec;
			divFact *= 2;
		}
	}
}

kernel void pathTracer(global uchar4 * img, global int * G, float4 cam_forward, float4 cam_up, float4 cam_right, float4 eye_offset, uint4 seeds){
	float4 color = (float4)(13, 13, 13, 0);
	int i = get_global_id(0);
	int j = get_global_id(1);
	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);
	float4 randValues1 = (float4)(MWC64XVEC2(&rng, 0.0f, 1.0f), MWC64XVEC2(&rng, 0.0f, 1.0f));
	float4 origin, destination, delta;
	for(int r = 64; r--;){
		delta = cam_up * ((randValues1.x - 0.5f) * 99) + cam_right * ((randValues1.y - 0.5f) * 99);
		origin = (float4)(17, 16, 8, 0) + delta;
		destination = NotOperator(delta * (-1) + (cam_up * (randValues1.z + i) + cam_right * (j + randValues1.w) + eye_offset) * 16);
		color = Sample(&origin, &destination, &rng, G) * 3.5f + color;
	}
	color.w = 255;
	//printf("Color %d %d: %f, %f, %f, %f\n", i, j, color.x, color.y, color.z, color.w);
	img[j*get_global_size(0)+i]=convert_uchar4(color);
}

