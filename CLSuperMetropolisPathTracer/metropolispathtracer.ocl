typedef struct{
	float4 v0;
	float4 v1;
	float4 v2;
} Triangle;

//A light path defined by 5 vertices
//The starting point is the light source position, but we do not need to memorize it
//The path length is defined as the number of edges
typedef struct{
	float4 v0;
	float4 v1;
	float4 v2;
	float4 v3;
	uchar length;
} Path;

//MWC64x, an RNG made by David B. Tomas, with custom seeding
//Source: http://cas.ee.ic.ac.uk/people/dt10/research/rngs-gpu-mwc64x.html

typedef struct{ uint2 x; uint2 c; } mwc64xvec2_state_t;

inline float2 MWC64XVEC2(mwc64xvec2_state_t *s, float leftLimit, float rightLimit)
{
    enum{ MWC64XVEC2_A = 4294883355U };
    uint2 x=s->x, c=s->c;
    uint2 res=x^c;                     // Calculate the result
    uint2 hi=mul_hi(x,MWC64XVEC2_A);              // Step the RNG
    x=x*MWC64XVEC2_A+c;
    c=hi+convert_uint2(x<c);
    s->x=x;
    s->c=c;             // Pack the state back up
    return leftLimit + convert_float2(res)*((rightLimit - leftLimit)/4294967295);
}

//Another simple RNG (often used in hashing) to randomize local id or global id
inline uint randomizeId(uint id)
{
        id = (id ^ 61) ^ (id >> 16);
        id *= 9;
        id = id ^ (id >> 4);
        id *= 0x27d4eb2d;
        id = id ^ (id >> 15);
        return id;
 }

//Mix seeds with randomized id
inline void MWC64XVEC2_Seeding(mwc64xvec2_state_t *s, uint4 seeds){
	const int i = get_global_id(1) * get_global_size(0) + get_global_id(0);
	s->x = (uint2)((seeds.x) ^ randomizeId(i), (seeds.y) ^ randomizeId(i));
	s->c = (uint2)((seeds.z) ^ randomizeId(i), (seeds.w) ^ randomizeId(i));
}

//Defined as operator! in the simple CPU tracer
inline float4 Normalize(float4 x){
	return ((1/sqrt(dot(x, x))) * x);
}

inline int TraceRay(float4 origin, float4 direction, float * t, float4 * normal, 
	local int * restrict Spheres, local int * restrict Planes, 
	local Triangle * restrict Triangles, int ntriangles){

	int m = 0;	//default material
	float rayDist;
	float4 intersection;

	//Triangle check vars
	Triangle curr_triangle;
	float4 edge0, edge2; 
	//Moller-Trumbore solution
	float4 pvec, qvec, tvec;
	float det, invDet, barycentric_u, barycentric_v;

	//Check for floor intersection
	rayDist = -origin.z / direction.z;
	if(.01f < rayDist && rayDist < *t){
		*t = rayDist;
		*normal = (float4)(0, 0, 1, 0);
		m = 1;
	}
	
	//Check for plane intersection
	for(int k = 19; k--;){
		for(int j = 9; j--;){
			if(Planes[j] & 1 << k){
				rayDist = (4+j-origin.z)/direction.z;
				intersection = origin + direction * rayDist;
				if(rayDist < *t && (fabs(k-intersection.x)<1) && fabs(intersection.y)<1){
				//if(dist < *t && distance(intersection, (float4)(k, 0, j+4, 0))<2){	//Circle intersection with euclidean distance
					*t = rayDist;
					*normal = (float4)(0, 0, 1, 0);
					m = 3;
				}
			}
		}
	}
	//Check for sphere intersection
	for(int k = 19; k--;){
		for(int j = 9; j--;){
			if (Spheres[j] & 1 << k){
				float4 p = origin + (float4)(-k, 0, -j - 4, 0);
				float b = dot(p, direction);
				float c = dot(p, p) - 1;
				float q = b * b - c;

				//Does the ray hit the sphere?
				if(q > 0){
					rayDist = -b - sqrt(q);
					//It does, compute the distance camera-sphere
					if(rayDist < (*t) && rayDist > 0.01f){
						*t = rayDist;
						*normal = Normalize(p + direction * (*t));
						m = 3;
					}
				}
			}
		}
	}
	
	//Check for triangle intersection (Moller-Trumbore)
	for(int i=0; i<ntriangles; i++){
		curr_triangle = Triangles[i];
		edge0 = curr_triangle.v1 - curr_triangle.v0;
		edge2 = curr_triangle.v2 - curr_triangle.v0;

		pvec = cross(direction, edge2);
		det = dot(edge0, pvec);
		if(fabs(det) < 0.01f)	continue;
		invDet = 1/det;
		tvec = origin - curr_triangle.v0;
		barycentric_u = dot(tvec, pvec) * invDet;
		if(barycentric_u < 0 || barycentric_u > 1) continue;
		qvec = cross(tvec, edge0);
		barycentric_v = dot(direction, qvec) * invDet;
		if (barycentric_v < 0 || barycentric_u+barycentric_v > 1) continue;
		rayDist = dot(edge2, qvec) * invDet;

		//Ray hits the triangle
		if(rayDist < *t){
			*t = rayDist;
			*normal = Normalize(cross(edge0, edge2));
			m = 4;
		}
	}
	
	return m;
}

inline float4 GetRandomDirection(mwc64xvec2_state_t rng){
	//Random direction algorithm from https://math.stackexchange.com/questions/44689/how-to-find-a-random-axis-or-unit-vector-in-3d/182650#182650
	float randSum = 2;
	float2 randValues;
	while(randSum >= 1){
		randValues = MWC64XVEC2(&rng, -1.0f, 1.0f);
		randSum = randValues.x*randValues.x + randValues.y*randValues.y;
	}
	return (float4)(2*randValues.x*sqrt(1-randSum), 2*randValues.y*sqrt(1-randSum), 1-2*randSum, 0);
}

inline void AddRandomVertex(float4 origin, float4 * vertexToAdd, Path * path, 
	local int * restrict lSpheres, local int * restrict lPlanes, 
	local Triangle * restrict lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	const float4 direction = GetRandomDirection(rng);
	float4 normal;
	float t = 1e9;	//default distance
	if(TraceRay(origin, direction, &t, &normal, lSpheres, lPlanes, lTriangles, ntriangles)){
		*vertexToAdd = origin + direction * t;
		path->length += 1;
	}
}

inline Path GetRandomPath(float4 origin, local int * restrict lSpheres, local int * restrict lPlanes, 
	local Triangle * restrict lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	Path curr_path;
	curr_path.length = 0;	//Default path length
	AddRandomVertex(origin, &(curr_path.v0), &curr_path, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	if (curr_path.length == 0){
		curr_path.v3 = curr_path.v2 = curr_path.v1 = curr_path.v0;
		return curr_path;
	}
	AddRandomVertex(curr_path.v0, &(curr_path.v1), &curr_path, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	if (curr_path.length == 1){
		curr_path.v3 = curr_path.v2 = curr_path.v1;
		return curr_path;
	}
	AddRandomVertex(curr_path.v1, &(curr_path.v2), &curr_path, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	if (curr_path.length == 2){
		curr_path.v3 = curr_path.v2;
		return curr_path;
	}
	AddRandomVertex(curr_path.v2, &(curr_path.v3), &curr_path, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	return curr_path;
}

//Commonly used perturbations from http://cg.iit.bme.hu/~szirmay/paper50_electronic.pdf
inline float4 Perturbation(float4 vertex, mwc64xvec2_state_t rng){
	float4 mutatedVertex;	
	const float4 randValues = (float4)(MWC64XVEC2(&rng, 0.0f, 1.0f),MWC64XVEC2(&rng, 0.0f, 1.0f).x, 0);
	const float4 s0 = (float4)(1.0f, 1.0f, 1.0f, 0);
	const float4 s1 = (float4)(1.0f/512.0f, 1.0f/512.0f, 1.0f/512.0f, 0);
	const float4 s2 = (float4)(1.0f/16.0f, 1.0f/16.0f, 1.0f/16.0f, 0);
	const float4 dx = s1 / (s1 / s2 + fabs(2.0f * randValues - s0)) - s1 / (s1 / s2 + s0);

	//Mutate vertex.x
	if (randValues.x < 0.5f){
		if(vertex.x < 1.0f) mutatedVertex.x = vertex.x + dx.x;
		else mutatedVertex.x = vertex.x + dx.x - 1.0f;
	}
	else{
		if(vertex.x < 0.0f) mutatedVertex.x = vertex.x - dx.x + 1.0f;
		else mutatedVertex.x = vertex.x - dx.x;
	}
	//Mutate vertex.y
	if (randValues.y < 0.5f){
		if(vertex.y < 1.0f) mutatedVertex.y = vertex.y + dx.y;
		else mutatedVertex.y = vertex.y + dx.y - 1.0f;
	}
	else{
		if(vertex.y < 0.0f) mutatedVertex.y = vertex.y - dx.y + 1.0f;
		else mutatedVertex.y = vertex.y - dx.y;
	}
	//Mutate vertex.z
	if (randValues.z < 0.5f){
		if(vertex.z < 1.0f) mutatedVertex.z = vertex.z + dx.z;
		else mutatedVertex.z = vertex.z + dx.z - 1.0f;
	}
	else{
		if(vertex.z < 0.0f) mutatedVertex.z = vertex.z - dx.z + 1.0f;
		else mutatedVertex.z = vertex.z - dx.z;
	}
	mutatedVertex.w = 0.0f;
	return mutatedVertex;
	
}

//Check if destination is the first intersection point of a ray that starts from origin in the direction Normalize(destination - origin)
inline int VerifyIntersection(float4 origin, float4 destination, local int * restrict lSpheres, 
	local int * restrict lPlanes, local Triangle * restrict lTriangles, int ntriangles){
	float t;
	float4 normal;
	const float4 direction = Normalize(destination - origin);
	const int m = TraceRay(origin, direction, &t, &normal, lSpheres, lPlanes, lTriangles, ntriangles);
	if (!m) return false;
	else{
		const float4 intersection = origin + direction * t;
		return (destination.x == intersection.x && destination.y == intersection.y && destination.z == intersection.z);
	}
}

//Mutate by adding vertices and perturbation
inline void Mutate(Path * seedpath, float4 origin, local int * lSpheres, local int * lPlanes, local Triangle * lTriangles, int ntriangles, mwc64xvec2_state_t rng){
	if(seedpath->length == 0){	//Path is empty, try and make a new one
		*seedpath = GetRandomPath(origin, lSpheres, lPlanes, lTriangles, ntriangles, rng);
		if(seedpath->length == 0) return;	//Still empty, try again next round
	}
	float2 randValues = MWC64XVEC2(&rng, 0.0f, 1.0f);
	const float mutation_probability = 1.0f/(seedpath->length+0.2f);
	if(mutation_probability < randValues.x) return;
	Path temp_path;
	if (seedpath->length == 1){
		//Try perturbation (100%), try adding 1 (40%), 2 (20%) or 3 (10%) vertices
		temp_path.v0 = Perturbation(seedpath->v0, rng);
		if (VerifyIntersection(origin, temp_path.v0, lSpheres, lPlanes, lTriangles, ntriangles)){
			seedpath->v0 = temp_path.v0;
		}
		if (randValues.y > 0.3f){
			AddRandomVertex(seedpath->v0, &(seedpath->v1), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
			if (seedpath->length == 1) return;
		}
		if (randValues.y > 0.7f){
			AddRandomVertex(seedpath->v1, &(seedpath->v2), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
			if (seedpath->length == 2) return;
		}
		if (randValues.y > 0.9f) AddRandomVertex(seedpath->v2, &(seedpath->v3), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	}
	else if (seedpath->length == 2){
		//Try perturbation (100%), try adding 1 (30%) or 2 (20%) vertices
		temp_path.v0 = Perturbation(seedpath->v0, rng);
		temp_path.v1 = Perturbation(seedpath->v1, rng);
		if (VerifyIntersection(origin, temp_path.v0, lSpheres, lPlanes, lTriangles, ntriangles) 
		&& VerifyIntersection(temp_path.v0, temp_path.v1, lSpheres, lPlanes, lTriangles, ntriangles)){
			seedpath->v0 = temp_path.v0;
			seedpath->v1 = temp_path.v1;
		}
		if (randValues.y < 0.3f){
			AddRandomVertex(seedpath->v1, &(seedpath->v2), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
			if (seedpath->length == 2) return;
		}
		if (randValues.y < 0.2f) AddRandomVertex(seedpath->v2, &(seedpath->v3), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	}
	else if (seedpath->length == 3){
		//Try perturbation (100%), try adding 1 vertex (20%)
		temp_path.v0 = Perturbation(seedpath->v0, rng);
		temp_path.v1 = Perturbation(seedpath->v1, rng);
		temp_path.v2 = Perturbation(seedpath->v2, rng);
		if (VerifyIntersection(origin, temp_path.v0, lSpheres, lPlanes, lTriangles, ntriangles) 
		&& VerifyIntersection(temp_path.v0, temp_path.v1, lSpheres, lPlanes, lTriangles, ntriangles)
		&& VerifyIntersection(temp_path.v1, temp_path.v2, lSpheres, lPlanes, lTriangles, ntriangles)){
			seedpath->v0 = temp_path.v0;
			seedpath->v1 = temp_path.v1;
			seedpath->v2 = temp_path.v2;
		}
		if (randValues.y < 0.2f) AddRandomVertex(seedpath->v2, &(seedpath->v3), seedpath, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	}
	else if (seedpath->length == 4){
		//Try perturbation
		temp_path.v0 = Perturbation(seedpath->v0, rng);
		temp_path.v1 = Perturbation(seedpath->v1, rng);
		temp_path.v2 = Perturbation(seedpath->v2, rng);
		temp_path.v3 = Perturbation(seedpath->v3, rng);

		if (VerifyIntersection(origin, temp_path.v0, lSpheres, lPlanes, lTriangles, ntriangles) 
		&& VerifyIntersection(temp_path.v0, temp_path.v1, lSpheres, lPlanes, lTriangles, ntriangles)
		&& VerifyIntersection(temp_path.v1, temp_path.v2, lSpheres, lPlanes, lTriangles, ntriangles)
		&& VerifyIntersection(temp_path.v2, temp_path.v3, lSpheres, lPlanes, lTriangles, ntriangles)){
			*seedpath = temp_path;
		}
		
	}
}

inline float4 Sample(float4 * origin, float4 * direction, mwc64xvec2_state_t * rng, 
	local int * restrict Spheres, local int * restrict Planes, 
	local Triangle * restrict Triangles, int ntriangles,
	global const float4 * restrict virtual_point_lights, int nvirtuallights, 
	local float4 * restrict scenelights, int nlights){
	//Recursion vars
	float4 colorFact = (float4)(0, 0, 0, 0);
	int divFact = 1;

	float2 randValues;
	float4 intersection, half_vec;
	float t;

	float4 normal, light_dir, light_pos;
	float distanceFromLight, light_intensity;
	float lamb_f, color, total_illumination = 0.0f;

	int material;
	for(int maxIter = 5; maxIter--;){
		t = 1e9;	//default distance
		material = TraceRay(*origin, *direction, &t, &normal, Spheres, Planes, Triangles, ntriangles);
		if (!material){
			//Nothing found and the ray goes upward: Generate a sky color
			return colorFact + (float4)(0.7f, 0.6f, 1.0f, 0) * pow(1 - (*direction).z, 4) / divFact;
		}

		//Something was hit
		intersection = (*origin) + (*direction) * t;

		//Compute total illumination factor by checking all virtual point lights
		for(int i=0; i<nvirtuallights; ++i){
			light_pos = virtual_point_lights[i];
			light_intensity = light_pos.w;
			if(light_intensity <= 0) continue;
			light_pos.w = 0;
			distanceFromLight = distance(light_pos, intersection);
			light_dir = (light_pos - intersection)/distanceFromLight;

			//Calculate the lambertian factor
			lamb_f = dot(light_dir, normal);
			t = distanceFromLight;
			//Calculate illumination factor (lambertian coefficient > 0 or in shadow)?
			//...Or just clamp it, shadows make it painfully slow because of the number of VLPs
			//if(lamb_f < 0 || TraceRay(intersection, light_dir, &t, &half_vec, Spheres, Planes, Triangles, ntriangles)){
			if(lamb_f < 0){
				lamb_f = 0;
			}
			else{
				//Objects away from the light should have less illumination (Inverse square law)
				total_illumination += lamb_f * min(light_intensity/(distanceFromLight*distanceFromLight), 1.0f);
			}
		}

		if(total_illumination > 1.0f) total_illumination = 1.0f;
		
		//Compute soft shadows with real lights
		for(int i=0; i<nlights; ++i){
			light_pos = scenelights[i];
			randValues = MWC64XVEC2(rng, 0.0f, 1.0f);
			light_pos.w = 0;
			distanceFromLight = distance(light_pos, intersection);
			light_dir = Normalize(light_pos + (float4)(randValues,0,0) + intersection * (-1));
			t = distanceFromLight;
			if(TraceRay(intersection, light_dir, &t, &half_vec, Spheres, Planes, Triangles, ntriangles)){
				total_illumination -= 1.0f/nlights;
			}
		}
		total_illumination /= 4;

		if(material == 1){
			//Nothing was hit and the ray was going downward: Generate floor checkerboard texture
			intersection = intersection * 0.2f;
			return colorFact+((int)(ceil(intersection.x) + ceil(intersection.y)) & 1 ? (float4)(3, 1, 1, 0) : (float4)(3, 3, 3, 0)) * (total_illumination) / divFact;
		}
		if(material == 3){	//diffuse shader
			float4 diffuseColor = (float4)(2, 3, 2, 0);
			return colorFact + (diffuseColor * (total_illumination)) / divFact;
		}
		if(material == 4){	//facing ratio
			return colorFact + max(0.0f, dot(normal, -(*direction)))/ divFact;
		}
		//m == 2 A reflective surface was hit. Cast a ray bouncing from it.
		//Attenuate color by 50% since it is bouncing (* 0.5)
		//Unrolled recursion with a loop and by updating those factors
		else{
			half_vec = (*direction) + normal * (dot(normal, *direction) * (-2));
			color = pow(dot(light_dir, half_vec) * (total_illumination > 0), 99);
			colorFact += (float4)(color, color, color, 0) * divFact;
			*origin = intersection;
			*direction = half_vec;
			divFact *= 2;
		}
	}
}

inline float4 SampleFromLightSource(float4 origin, float4 direction, 
	local int * restrict Spheres, local int * restrict Planes, 
	local Triangle * restrict Triangles, int ntriangles,
	float light_intensity, int total_paths){

	float t;

	float4 normal, intersection;
	float distanceFromLight, lamb_f;

	t = 1e9;	//default distance
	int material = TraceRay(origin, direction, &t, &normal, Spheres, Planes, Triangles, ntriangles);
	if (!material){
		//No surface or floor found: return dummy light source
		return (float4)(0, 0, 0, 0);
	}

	//Something was hit
	intersection = origin + direction * t;

	//Calculate the lambertian factor
	lamb_f = dot(direction, normal);

	//Clamp to 0
	if(lamb_f < 0){
		lamb_f = 0;
	}
	else{
		//Objects away from the light should have less illumination (Inverse square law)
		distanceFromLight = distance(origin, intersection);
		lamb_f = lamb_f * min(light_intensity/(distanceFromLight*distanceFromLight), 1.0f);
	}
	//Clamp to 1
	if(lamb_f > 1.0f) lamb_f = 1.0f;

	//return a light at position intersection with an intensity proportional to the number of VLPs
	if(material == 1){
		//The ray was going downward: return floor light
		return (float4)(intersection.s012, 100*lamb_f/(total_paths/256));
	}
	if(material == 2){
		//The ray hits a specular surface, which should reflect most light: return a dim light
		return (float4)(intersection.s012, 10*lamb_f/(total_paths/256));
	}
	if(material == 3){	//diffuse shader
		return (float4)(intersection.s012, 40*lamb_f/(total_paths/256));
	}
	return (float4)(0, 0, 0, 0);
}

//Compute random paths through the scene as seeds for the second phase
kernel void lightTracer(global const int * restrict Spheres, global const int * restrict Planes, 
	global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict scenelights, int nlights,
	global Path * restrict seedpaths, uint4 seeds,
	local int * restrict lSpheres, local int * restrict lPlanes, 
	local Triangle* restrict lTriangles, local float4 * restrict lScenelights){
	int gi = get_global_id(0);
	int gws = get_global_size(0);
	int li = get_local_id(0);

	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);

	float4 current_light;
	float4 origin;

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lPlanes[li]=Planes[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}

	if(li < ntriangles){
		lTriangles[li]=Triangles[li];
	}
	barrier(CLK_LOCAL_MEM_FENCE);
	//for each light, create a path launching rays in random directions
	for(int l=0; l<nlights; ++l){
		current_light = lScenelights[l];
		origin = (float4)(current_light.s012, 0);	//Get position in 3D space of current light
		seedpaths[gi+l*gws] = GetRandomPath(origin, lSpheres, lPlanes, lTriangles, ntriangles, rng);
	}
}

//Uses seed paths from lightTracer to find more light paths
kernel void MetropolisLightTracer(global const int * restrict Spheres, global const int * restrict Planes, 
	global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict scenelights, int nlights,
	global Path * restrict seedpaths, global float16 * restrict virtual_point_lights, 
	uint4 seeds, int mutation_rounds,
	local int * restrict lSpheres, local int * restrict lPlanes, 
	local Triangle* restrict lTriangles, local float4 * restrict lScenelights){
	int gi = get_global_id(0);
	int gws = get_global_size(0);
	int li = get_local_id(0);
	int total_paths = gws * nlights;	//Total number of paths

	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);

	float4 current_light;
	float light_intensity;
	float4 origin, direction;

	Path seedpath;
	float4 curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3;

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lPlanes[li]=Planes[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}

	if(li < ntriangles){
		lTriangles[li]=Triangles[li];
	}
	barrier(CLK_LOCAL_MEM_FENCE);
	//for each light, create get the sample VLPs from the mutated seed path
	for(int l=0; l<nlights; ++l){
		current_light = lScenelights[l];
		origin = (float4)(current_light.s012, 0);	//Get position in 3D space of current light
		light_intensity = current_light.w;
		seedpath = seedpaths[gi+l*gws];

		//Initialize lights in case the path gets cut off
		curr_vlp0 = (float4)(0);
		curr_vlp1 = (float4)(0);
		curr_vlp2 = (float4)(0);
		curr_vlp3 = (float4)(0);

		for(int m = 0; m < mutation_rounds; ++m){
			Mutate(&seedpath, origin, lSpheres, lPlanes, lTriangles, ntriangles, rng);
		}
		if(seedpath.length == 0){	//Still empty after all the attempts, give up: do not want to force it (in case the scene is empty)
			virtual_point_lights[gi+l*gws] = (float16)(curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3);
			continue;
		}

		//First vertex of the light path (after the light origin)
		direction = Normalize(seedpath.v0 - origin);
		curr_vlp0 = SampleFromLightSource(origin, direction, lSpheres, lPlanes, lTriangles, ntriangles, light_intensity, total_paths);
		if(curr_vlp0.w == 0){	//Sky was hit, stop the path here
			virtual_point_lights[gi+l*gws] = (float16)(curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3);
			continue;
		}

		//Second vertex of the light path
		origin = (float4)(curr_vlp0.s012, 0);	//Intersection point of the previous ray
		direction = Normalize(seedpath.v1 - seedpath.v0);
		curr_vlp1 = SampleFromLightSource(origin, direction, lSpheres, lPlanes, lTriangles, ntriangles, light_intensity/2, total_paths);	//Decrease light intensity because it is bouncing
		if(curr_vlp1.w == 0){	//Sky was hit, stop the path here
			virtual_point_lights[gi+l*gws] = (float16)(curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3);
			continue;
		}

		//Third vertex of the light path
		origin = (float4)(curr_vlp1.s012, 0);	//Intersection point of the previous ray
		direction = Normalize(seedpath.v2 - seedpath.v1);
		curr_vlp2 = SampleFromLightSource(origin, direction, lSpheres, lPlanes, lTriangles, ntriangles, light_intensity/4, total_paths);	//Decrease light intensity because it is bouncing
		if(curr_vlp2.w == 0){	//Sky was hit, stop the path here
			virtual_point_lights[gi+l*gws] = (float16)(curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3);
			continue;
		}

		//Fourth vertex of the light path
		origin = (float4)(curr_vlp2.s012, 0);	//Intersection point of the previous ray
		direction = Normalize(seedpath.v3 - seedpath.v2);
		curr_vlp3 = SampleFromLightSource(origin, direction, lSpheres, lPlanes, lTriangles, ntriangles, light_intensity/8, total_paths);	//Decrease light intensity because it is bouncing
		virtual_point_lights[gi+l*gws] = (float16)(curr_vlp0, curr_vlp1, curr_vlp2, curr_vlp3);
	}
}

kernel void pathTracer(global uchar4 * restrict img, global const int * restrict Spheres, 
	global const int * restrict Planes, global const Triangle * restrict Triangles, int ntriangles, 
	global const float4 * restrict virtual_point_lights, int nvlp,
	global const float4 * restrict scenelights, int nlights,
	float4 cam_forward, float4 cam_up, float4 cam_right, float4 eye_offset, uint4 seeds,
	local int * restrict lSpheres, local int * restrict lPlanes, 
	local Triangle* restrict lTriangles, local float4 * restrict lScenelights){
	float4 color = (float4)(13, 13, 13, 0);
	int i = get_global_id(0);
	int j = get_global_id(1);
	int li = get_local_id(0) + get_local_id(1) * get_local_size(0);
	mwc64xvec2_state_t rng;
	MWC64XVEC2_Seeding(&rng, seeds);
	float4 randValues;
	float4 origin, direction, delta;

	if (li < 9){
		lSpheres[li]=Spheres[li];
		lPlanes[li]=Planes[li];
	}

	if(li < ntriangles){
		lTriangles[li]=Triangles[li];
	}

	if(li < nlights){
		lScenelights[li]=scenelights[li];
	}
	barrier(CLK_LOCAL_MEM_FENCE);
	for(int r = 64; r--;){
		randValues = (float4)(MWC64XVEC2(&rng, 0.0f, 1.0f), MWC64XVEC2(&rng, 0.0f, 1.0f));
		delta = cam_up * ((randValues.x - 0.5f) * 99) + cam_right * ((randValues.y - 0.5f) * 99);
		origin = (float4)(17, 16, 8, 0) + delta;
		direction = Normalize(delta * (-1) + (cam_up * (randValues.z + i) + cam_right * (j + randValues.w) + eye_offset) * 16);
		color = Sample(&origin, &direction, &rng, lSpheres, lPlanes, lTriangles, ntriangles, virtual_point_lights, nvlp, lScenelights, nlights) * 3.5f + color;
	}
	color.w = 255;
	img[j*get_global_size(0)+i]=convert_uchar4(color);
}

